#version 330

in vec3 vertex_position;
in vec3 vertex_normal;
in vec2 vertex_texture;
in vec3 vertex_tangent;
in vec3 vertex_bitangent;

out vec2 TexCoord;
out mat4 view_matrix;
out vec3 TangentLightPos;
out vec3 TangentViewPos;
out vec3 TangentFragPos;
out vec3 ViewFragPos;

uniform mat4 view;
uniform mat4 proj;
uniform mat4 model;
uniform vec4 LightPosition;

void main(){
  

  mat4 ModelViewMatrix = view * model;
  
  // Position in view space
  vec3 eyeCoords = vec3(ModelViewMatrix * vec4(vertex_position,1.0));
  ViewFragPos = eyeCoords;

  TexCoord = vertex_texture;
  view_matrix = view;

  // Calculate TBN matrix for normal mapping
  vec3 T = normalize(vec3(model * vec4(vertex_tangent, 0.0)));
  vec3 N = normalize(vec3(model * vec4(vertex_normal, 0.0)));

  // Gram-Schmidt process to re-orthogonalize
  T = normalize(T - dot(T, N) * N);

  vec3 B = cross(N, T);
  mat3 TBN = transpose(mat3(T, B, N));
  TangentLightPos = TBN * LightPosition.xyz;
  TangentViewPos = TBN * eyeCoords;
  TangentFragPos = TBN * vec3(model * vec4(vertex_position, 1.0));
  
  // Convert position to clip coordinates and pass along
  gl_Position = proj * vec4(eyeCoords, 1.0);
}


  