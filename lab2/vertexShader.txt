#version 330

in vec3 vertex_position;
in vec3 vertex_normal;

out vec3 eyeCoords, tnorm;
out mat4 view_matrix;
out vec3 RaR, RaG, RaB, Re;
out vec3 I;

uniform mat4 view;
uniform mat4 proj;
uniform mat4 model;
uniform float eta = 0.8;
uniform float chromatic = 0.0;

void main(){

  mat4 ModelViewMatrix = view * model;
  
  eyeCoords = vec3(ModelViewMatrix * vec4(vertex_position, 1.0));

  vec3 worldPos = vec3(model * vec4(vertex_position, 1.0));
  vec3 worldNormal = normalize(mat3(transpose(inverse(model))) * vertex_normal);
  
  vec3 cameraPos = -transpose(mat3(view)) * vec3(view[3]);
  I = normalize(worldPos - cameraPos);
  
  tnorm = worldNormal;
  
  view_matrix = view;

  float scaledChromatic = (1.0 - eta) * chromatic;
  
  Re = reflect(I, worldNormal);
  RaR = refract(I, worldNormal, eta - 0.1 * scaledChromatic);
  RaG = refract(I, worldNormal, eta);
  RaB = refract(I, worldNormal, eta + 0.1 * scaledChromatic);
  
  gl_Position = proj * vec4(eyeCoords, 1.0);
}